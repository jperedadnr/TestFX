/*
 * Copyright 2013-2014 SmartBear Software
 * Copyright 2014-2024 The TestFX Contributors
 *
 * Licensed under the EUPL, Version 1.1 or - as soon they will be approved by the
 * European Commission - subsequent versions of the EUPL (the "Licence"); You may
 * not use this work except in compliance with the Licence.
 *
 * You may obtain a copy of the Licence at:
 * http://ec.europa.eu/idabc/eupl
 *
 * Unless required by applicable law or agreed to in writing, software distributed
 * under the Licence is distributed on an "AS IS" basis, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the Licence for the
 * specific language governing permissions and limitations under the Licence.
 */

publishing {
	repositories {
		maven {
			name 'gluonReleases'
			def releasesRepoUrl = "https://nexus.gluonhq.com/nexus/content/repositories/releases/"
			def snapshotsRepoUrl = "https://nexus.gluonhq.com/nexus/content/repositories/public-snapshots/"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username = project.hasProperty('gluonNexusDeployUsername') ? project.property('gluonNexusDeployUsername') : ''
				password = project.hasProperty('gluonNexusDeployPassword') ? project.property('gluonNexusDeployPassword') : ''
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from components.java
			afterEvaluate {
				artifact sourceJar
				artifact javadocJar
			}
			description 'Generate the artifact pom file for Maven Central'
			pom {
				name = "${project.name}"
				description = "${project.description}"
				inceptionYear = '2013'
				url = "http://testfx.org/"

				licenses {
					license {
						name = "European Union Public Licence, Version 1.1"
						url = "http://ec.europa.eu/idabc/eupl.html"
						distribution = "repo"
					}
				}

				scm {
					url = "https://github.com/TestFX/TestFX"
				}

				developers {
					developer {
						id = "mvsoder"
						name = "Mark Soderquist"
						url = "https://github.com/mvsoder"
						timezone = "-7"
						roles = ["developer"]
					}

					developer {
						id = "svenruppert"
						name = "Sven Ruppert"
						url = "https://github.com/svenruppert"
						timezone = "+1"
						roles = ["developer"]
					}

					developer {
						id = "minisu"
						name = "Henrik Olsson"
						url = "https://github.com/minisu"
						timezone = "+1"
						roles = ["former developer"]
					}

					developer {
						id = "dainnilsson"
						name = "Dain Nilsson"
						url = "https://github.com/dainnilsson"
						timezone = "+1"
						roles = ["project founder",  "former developer"]
					}

					developer {
						id = "hastebrot"
						name = "Benjamin Gudehus"
						url = " https://github.com/hastebrot"
						timezone = "+1"
						roles = ["project maintainer"]
					}
				}

				// somehow the groupId of the javafx dependencies is missing, add it:
				withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies.'*'.findAll() {
						it.artifactId.text().startsWith('javafx')
                    }.each {
						it.appendNode('groupId', 'org.openjfx')
                    }
				}
			}
		}
				//.writeTo("$buildDir/libs/${project.name}-${project.version}.pom")
	}
}
